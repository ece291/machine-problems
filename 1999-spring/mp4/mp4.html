<html>
<head>
<title>ECE291 Machine Problem 4</title>
</head>

<body bgcolor=ffffff>

<table width=100% border=0>
<tr><td align=left   width=33%>ECE291</td>
    <td align=center width=34%>Computer Engineering II</td>
    <td align=right  width=33%>Lockwood, Spring 1999</td></tr></table>
<center><h1>Machine Problem 4</h1>
       <h2>Graphic and high-speed image processing routines</h2>
</center>

<p>
<center><table border=1 width=60%>
 <tr><td align=right><b>Assigned</b></td>
    <td>Thursday, March 25, 1999 </td></tr>
 <tr><td align=right><b>Due Date</b></td>
    <td>Tuesday, April 6, 1999, 5pm</td></tr>
   <tr><td align=right><b>Purpose: </b></td>
    <td>Video graphics and animation 
</td></tr>
   <tr><td align=right><b>Points</b></td><td>50</td></tr>
</table></center>

<p>
<!-- Copyright 1996-1999, John W Lockwood, All rights reserved
     For usage information: contact lockwood@ipoint.vlsi.uiuc.edu or visit
     http://ipoint.vlsi.uiuc.edu/~lockwood/
-->

<p>
<h2>Introduction</h2>
<p>
Over the course of the next two machine problems,
we will implement the game of tag in a virtual environment.  
The rules of tag are simple.  The person that is <i>it</i>
chases after everyone else.  Upon <i>tagging</i> someone, 
that person become <i>it</i>.
<p>
Our game will be played in the safety of the ECE291 lab
using Ethernet and VGA video graphics.  
For MP4, we will implement the graphic 
functions to represent ourself in the game as
a bitmapped image (BMP).  Our program will allow us
to move our icon over a mode-13h VGA graphics display. 
<p>
For MP5 (the 2nd half of this assignment),
we will use NetBIOS's datagram group service to 
transmit our images and 
movements to the other players on the network. 
Each player sees an identical view of the game from their own machine.
Up to 63 players can play the game at once.
<p>

<h2>Video Graphics Implementation</h2>
To create complex graphic images, it is convienient to use existing 
software tools and standard image file formats.  For this MP, we will use
<i>Corel PhotoPaint</i> to create
BMP files.  To standardize the palette entries, 
we will limit ourselves to 16-colors of the default palette.  
All images for this MP have the dimensions of 32x32 pixels.  
<p>
For this machine problem, we will write the following
routines to load, store, and draw these images on the screen.
A screen dump of the running program is shown below:
<p>
  <img src="screendump.gif" width="652" height="459">
<p>

<h2>Video Graphic Routines</h2>
<ul>
<li><i>All routines should preserve the values of
       any registers modified</i>
<li><i>Use proper segments where needed</i><p>
<li><font size=+2><b>DrawBox</b></font>
  <ul>
  <li><b>Purpose:</b> Draw a filled square on the screen
  <li><b>Inputs: </b>
    <ul>
    <li>AL: Color
    <li>DI: Screen location (top-left corner)
    </ul>
  <li><b>Output: </b> Writes directly to screen
  <li><b>Description</b>: 
    This routine draws a 32x32 filled square on the screen
    using the palette number specified in AL.
  <li><b>Notes</b>:
    <ul>
    <li>This will be a frequently-used routine for this MP.  <br>
       It should be optimized for performance using a 32-bit register 
       (386, 486) and string operations.
    </ul>
  </ul><p>

<li><font size=+2><b>DrawEmptyBox</b></font>
  <ul>
  <li><b>Purpose:</b> Draw an empty square box on the screen
  <li><b>Inputs: </b>
    <ul>
    <li>AL: Color
    <li>DI: Screen location (top-left corner)
    </ul>
  <li><b>Output: </b> Writes directly to screen
  <li><b>Description</b>: 
    This routine draws an empty 32x32 square box
    on the screen using the palette number specified in AL.
    Pixels inside the box should not be affected by this call.
  <li><b>Notes</b>:
    <ul>
    <li>This will be a frequently-used routine for this MP.  <br>
       It should be optimized for performance using a 32-bit register 
       (386, 486) and string operations where possible.
    </ul>
  </ul><p>



<li><font size=+2><b>DrawBMP</b></font>
  <ul>
  <li><b>Purpose:</b> Write an image to the screen
  <li><b>Inputs: </b>
    <ul>
    <li>AX: Image Number (0..63)
    <li>DI: Screen location (top-left corner)
    <li>Variable <tt>ImageArray</tt>
    </ul>
  <li><b>Output: </b> Writes directly to screen
  <li><b>Description</b>: 
    This routine draws the AX<i>th</i> image from <tt>ImageArray</tt>
    to the VGA display.  <br>
  DI is specified as a offset on a mode-13h screen.  
  <li><b>Notes</b>:
    <ul>
    <li>This will be a frequently-used routine for this MP.  <br>
       It should be optimized for performance using a 32-bit variant
       of the repeating string operations.
    </ul>
   </ul><p>

<li><font size=+2><b>LoadBMP</b></font>
  <ul>
  <li><b>Purpose:</b> Load a BMP file from disk to memory
  <li><b>Input: </b>
    DX: Pointer to the null-terminated string with the file name
  <li><b>Output: </b>
    Contents of file stored in Variable BMP
  <li><b>Description:</b> This routine uses DOS <tt>INT 21h</tt> 
    services to open and read a file from the disk.  
    The contents of the file should be stored in the variable called BMP.  
  <li><b>Notes:</b>
    <ul>
    <li>The variable BMP is located in a different segment. <br>
        The segment can be accessed as <tt>Seg BMP</tt>.
    <li>The structure of the BMP file  is given to you in the source code.
    <li>Null-terminated strings end with ASCII 0.
    <li>If the file cannot be read, switch back to text-mode video
        and print an error message and terminate the program.
    </ul>
  </ul><p>

<li><font size=+2><b>StoreBMP</b></font>
  <ul>
  <li><b>Purpose:</b> Convert BMP data and store to image array.
  <li><b>Inputs: </b>
    <ul>
    <li>AX: Image Number (0..63)
    <li>DS:SI: Pointer to BMP data (Segment:Offset)
    </ul>
  <li><b>Output: </b>
    1024 bytes written to AX<i>th</i> element of <tt>ImageArray</tt>
  <li><b>Description</b>: 
      This routine converts a 16-color, 32x32-pixel BMP image to 
      a VGA mode-13h formatted array of bytes and stores the data
      in an array.   
  <li><b>Notes:</b> <ul>
     <li>The variable <tt>ImageArray</tt> is located in a different segment
     <li>BMP files store rows from the bottom up.
     <li><tt>ImageArray</tt> uses consecutive memory locations to store
         64 images of 1024 bytes in row-column format.
     <li>The 16-color palette of the BMP images map directly into
         the default 256-color palette of the VGA.
    </ul>
  </ul><p>
<li><font size=+2><b>MyImage.bmp</b></font>
  <ul>
  <li><b>Purpose:</b> Your virtual representation 
  <li><b>Inputs: </b> Artistic creation skills 
  <li><b>Output: </b> 32x32, 16-color BMP image 
  <li><b>Description</b>: 
    Use the Corel PhotoPaint program
    (or another other graphic editing program of your choice), 
   to create an accurate image that represents yourself
    in the network tag game.  This image should resemble
    yourself and be suitable for display to all members of the class.
  <li><b>Notes</b>:
    <ul>
    <li>It is <i>required</i> that your image resemble your face<br>
        to the degree that everyone in the class will be able<br>
        to match your image with your face.
    <li>Your image <i>must</i> have the exact size of
        Height=32 pixels, Width=32 pixels
    <li>Your image <i>must</i> use the same 16-color palette 
        as the given images in this MP.  
    <li>Save your image as an uncompressed windows BMP file
        called <tt>MyImage.bmp</tt>.  
    <li>The length of your image file <i>must</i> be exactly <br>
            = 118 header bytes + (32*32 image pixels)/(2 pixels/byte) <br>
            = 118 header bytes + 512 image bytes <Br>
            = 630 bytes.
    </ul>
  <li><b>Hints</b>:
    <ul>
    <li>By loading an existing image from the MP4 directory,
        Corel PhotoPaint will be initialized 
        with an image template that has the right size,
        correct palette, and proper color depth.
    <li>In Corel PhotoPaint,
        you can view the palette from the <br> 
           Image--ColorTable menu.<br>
        You should not change the palette entries from the values given.
    <li>You can cut-and-paste images into your tempplate window.
        Be aware, however, that some work is needed to resample  
         the image resolution and remap the palette.
    </ul>

  </ul>
</ul>

<hr>
<h2>Points</h2>

You will earn the following points for replacing each of the subroutines
with your own code. 
 
<ul>
<li>DrawBox: 8 pts
<li>DrawEmptyBox: 8 pts
<li>DrawBMP: 8 pts
<li>LoadBMP: 8 pts
<li>StoreBMP: 8 pts
<li>Myimage.bmp: 10 pts 
</ul>


<h2>Preliminary Procedure </h2> 
<ul>
<li>You will begin MP4 with the following files:
    <ul>
    <li><tt>MP4.EXE</tt>: Fully functional program using library functions
    <li><tt>MP4.ASM</tt>: Graphic Functions Framework
    <li><tt>LIBMP4.LIB</tt>: Working versions of all routines
    <li><tt>LIB291.LIB</tt>: Useful (and free) routines 
    <li><tt>MYIMAGE.BMP</tt>: A really ugly BMP file
    <li><tt>DOG.BMP</tt>: Picture of a dog
    <li><tt>Makefile</tt>
    </ul>

<li>You can obtain these files via one of the methods listed below:
  <ul>
  <li>In lab, copy all files from the MP4 directory to your directory 
      with the following command:<br>
      <tt>xcopy /s V:\ece291\mp4\ W:\mp4</tt> <br>
  <li>Alternatively, 
      download all files as: <a href="mp4.zip">MP4.ZIP</a> 
  </ul>
</ul>


<hr>
<h2>MP4.ASM</h2>
<xmp>

        PAGE 75, 132
        TITLE  MP4:Network Tag Graphics - Your Name

Comment *
        NETWORK TAG - Graphic Routines
        ------------------------------
        ECE291: Machine Problem 4
        Prof. John W. Lockwood
        University of Illinois,
        Dept. of Electrical & Computer Engineering
        Spring 1999
        Revision 2.0
        *

.MODEL LARGE  ; Allow Multiple Segments to be defined
.486          ; Enable code with 32-bit registers and 486 CPU Features



; ===== Handy General MACROS & Constants =================================

CR      EQU 13         ; ASCII Carriage Return
LF      EQU 10         ; ASCII LineFeed

VidGrSEG EQU 0A000h

GMODE MACRO            ; Set VGA to Mode=13h VGA Graphics (320x200)
        PUSH AX
        mov ah,00h
        mov al,13h
        int 10h
        POP AX
ENDM

TMODE MACRO            ; Set VGA for Text Mode (80x25 characters)
        PUSH AX
        mov ah,00h
        mov al,02h
        int 10h
        POP AX
ENDM

;====== Bitmap structure definition =======================================

BMPTYPE STRUC
; --- BitMapFileHeader --- ; 14 bytes
  BFType            DB 'BM' ; File Type
  BFSize            DD   ?  ; File Size (in Bytes)
  BFReserved1       DW   0  ; Reserved 
  BFReserved2       DW   0  ;    "
  BFOffBits         DD   ?  ; Offset to start of image data 
; --- BitMapInfoHeader --- ; 40 bytes
  BISize            DD   ?  ; Size of BitMapInfoHeader (28h = 40d)
  BIWidth           DD   ?  ; # Pixel Rows 
  BIHeight          DD   ?  ; # Pixel Columns
  BIPlanes          DW   1  ;
  BIBitCount        DW   ?  ; Log2(palette size) = 4 for 16-color image
  BICompression     DD   0  ; RGB = 0 = Uncompressed 
  BISizeImage       DD   ?  ; Size of Image (Bytes)
  BIXPelsPerMeter   DD   ?  ;
  BIYPelsPerMeter   DD   ?  ;
  BIColorsUsed      DD   0  ; 0=All
  BIColorsImportant DD   0  ;
; --- Color Table --- ; 64 Bytes
  RGBQuad           DB 16 dup ( 4 dup(?) ) ; Blue, Green, Red, Unused
; --- Image Data Follows ; n Bytes
  ImageData         DB 512 dup(0) ; Image data (bottom row first)
BMPTYPE ENDS

;====== Externals ========================================================

; -- LIB291 Routines (Free) --

extrn dspmsg:near, dosxit:near, kbdin:near

; -- LIBMP4.LIB Routines (Write these) --

extrn LibLoadBMP:near
extrn LibStoreBMP:near
extrn LibDrawBMP:near
extrn LibDrawBox:near
extrn LibDrawEmptyBox:near

extrn MPXit:near

;====== PUBLICs ===========================================================

  PUBLIC bmp        ; Must be available for linkage with LIBMP4 library
  PUBLIC ImageArray

;====== Stack Segment =====================================================

stkseg  segment stack                 ; *** STACK SEGMENT ***

        db      64 dup ('STACK   ')   ; Allocate bytes of stack memory

stkseg  ends

;====== BMP Segment =======================================================

bmpseg  segment public                ; *** BMP Segment ***

  bmp bmptype < >                     ; Allocate bmp to hold 1 BMP image

bmpseg ends

;====== Image Segment =====================================================
                         
imageseg segment public               ; *** IMAGE Segment ***

  ImageArray db 64 dup( 1024 dup(1) ) ; Allocate memory to hold images

    ; 64 Images of 1024 bytes, each colored blue (palette #1)

imageseg ends


;====== Code/Data segment ================================================

cseg    segment public 'CODE'         ; *** CODE SEGMENT ***
        assume  cs:cseg, ds:cseg, ss:stkseg, es:nothing

crlf    db CR,LF,'$'

;====== Procedures =======================================================

; Your code goes here !

LoadBMP  PROC NEAR
         Call LibLoadBMP       ; Replace with your code!
         RET
LoadBMP  ENDP


StoreBMP PROC NEAR
         Call LibStoreBMP      ; Replace with your code!
         RET
StoreBMP ENDP


DrawBMP  PROC NEAR
         Call LibDrawBMP       ; Replace with your code!
         RET
DrawBMP  ENDP


DrawBox  PROC NEAR
         Call LibDrawBox       ; Replace with your code!
         RET                   
DrawBox  ENDP


DrawEmptyBox PROC NEAR
         Call LibDrawEmptyBox  ; Replace with your code!
         RET
DrawEmptyBox ENDP

;====== Main =============================================================

MyImage  db 'MyImage.BMP',0          ; Null-terminated Filename
DogImage db 'Dog.BMP',0

Main PROC far
        MOV AX,CS
        MOV DS,AX

        ; The following code exercises the graphic functions
        ; that you will be using for the network tag game.
        ; Use this code to verify that your routines implement the
        ; same functionality as that of the library routines!

        MOV     DX, offset MyImage
        Call LoadBMP               ; Load MyImage.BMP image from disk

        PUSH    DS
        MOV     AX,Seg BMP
        MOV     DS,AX
        MOV     SI,Offset BMP
        MOV     AX,0
        Call StoreBMP              ; Store and expand BMP image to
        POP     DS                 ; Location 0 of Image array

        MOV DX,offset DogImage
        Call LoadBMP               ; Load Dog.BMP from disk

        PUSH    DS
        MOV     AX,Seg BMP
        MOV     DS,AX
        MOV     SI,Offset BMP
        MOV     AX,5               ; Store and expand BMP image to
        Call StoreBMP              ; Location 5 of Image array
        POP     DS

        GMODE

        MOV     DI,0
        MOV     AL,8+4; Bright Red
        Call DrawBOX               ; Draw box at top-left of screen

        Call KBDin ; Wait for keypress

        MOV     DI,32
        MOV     AL,8+7; Bright White
        Call DrawEmptyBOX          ; Draw White empty box

        Call KBDin ; Wait for keypress

        MOV     DI,64
        MOV     AL,8+1; Bright Blue
        Call DrawBOX               ; Draw Blue box

        Call KBDin ; Wait for keypress

        MOV     AX,5  ; Dog
        MOV     DI,320-32          ; Draw dog at right of screen
        Call DrawBMP

        Call KBDin ; Wait for keypress

        MOV     AX,0  ; MyImage
        MOV     DI,(200-32)*320
        Call DrawBMP               ; Draw MyImage at bottom, left of screen

        Call KBDin ; Wait for keypress


        MOV     DI,(200-32)*320+320-32
        MOV     AL,2  ; Green
        Call DrawBOX               ; Draw Green box at bottom, right of screen

        Call KBDin ; Wait for keypress

        MOV     DI,320*32+16
        MOV     AX,5  ; Dog
        Call DrawBMP

        Call KBDin ; Wait for keypress

        MOV     AL,4  ; Red
        Call DrawEmptyBox          ; Draw dog in empty red box

        Call KBDin ; Wait for keypress

        MOV     DI,320*32+48
        MOV     AX,0  ; Dog
        Call DrawBMP

        Call KBDin ; Wait for keypress

        MOV     AL,2  ; Green      ; Draw MyImage in empty green box
        Call DrawEmptyBox

        Call KBDin ; Wait for keypress

                                     ; Animate image across screen

        MOV DI,320*100 + 320-32      ; Start at Row=100, Right side of screen
        MOV CX,320-32                ; Move across entire width of screen

        MOV AX,0000h
        MOV ES,AX       ; Load ES:SI with addres of system timer
        MOV SI,046Ch    ; (increments 18 times/second)
        MOV EDX,ES:[SI] ; Load EDX with current tick count.


Animate:MOV     AX,5
        Call    DrawBMP

Waiting:CMP EDX,ES:[SI] ; Pause until time increments by one tick
        JE Waiting      ; ( tick will increment every 1/18 of second)
        MOV EDX,ES:[SI]

        MOV     AL,0
        Call    DrawBOX
        DEC DI          ; Animate movement
        LOOP Animate

        Call Kbdin      ; Wait for keypress

        TMODE           ; Switch back to text-mode video

        Call MPXit      ; Exit

Main ENDP

;====================================================================

cseg    ENDS
        END MAIN
</xmp>
</body>


